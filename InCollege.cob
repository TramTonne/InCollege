IDENTIFICATION DIVISION.
PROGRAM-ID. INCOLLEGE.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT INPUT-FILE ASSIGN TO "InCollege-Input.txt"
        ORGANIZATION IS LINE SEQUENTIAL.
    SELECT OUTPUT-FILE ASSIGN TO "InCollege-Output.txt"
        ORGANIZATION IS LINE SEQUENTIAL.
    SELECT ACCOUNTS-FILE ASSIGN TO "accounts.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS ACCOUNTS-STATUS.
    SELECT PROFILES-FILE ASSIGN TO "profiles.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS PROFILES-STATUS.
    SELECT TEMP-PROFILES-FILE ASSIGN TO "temp-profiles.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS TEMP-PROFILES-STATUS.
    SELECT CONNECTIONS-FILE ASSIGN TO "connections.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS CONNECTIONS-STATUS.
    SELECT TEMP-CONNECTIONS-FILE ASSIGN TO "temp-connections.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS TEMP-CONNECTIONS-STATUS.
DATA DIVISION.
FILE SECTION.
FD INPUT-FILE.
01 INPUT-RECORD PIC X(80).
FD OUTPUT-FILE.
01 OUTPUT-RECORD PIC X(80).
FD ACCOUNTS-FILE.
01 ACCOUNT-RECORD.
   05 ACCOUNT-USERNAME PIC X(20).
   05 ACCOUNT-PASSWORD PIC X(12).
   05 ACCOUNT-PASSWORD-LENGTH PIC 9(2).
FD PROFILES-FILE.
01 PROFILE-RECORD.
   05 PROFILE-USERNAME PIC X(20).
   05 PROFILE-FIRST-NAME PIC X(50).
   05 PROFILE-LAST-NAME PIC X(50).
   05 PROFILE-UNIVERSITY PIC X(100).
   05 PROFILE-MAJOR PIC X(50).
   05 PROFILE-GRAD-YEAR PIC X(4).
   05 PROFILE-ABOUT PIC X(200).
   05 PROFILE-EXP-COUNT PIC 9.
   05 PROFILE-EXPERIENCE OCCURS 3 TIMES.
      10 EXP-TITLE PIC X(100).
      10 EXP-COMPANY PIC X(100).
      10 EXP-DATES PIC X(50).
      10 EXP-DESC PIC X(200).
   05 PROFILE-EDU-COUNT PIC 9.
   05 PROFILE-EDUCATION OCCURS 3 TIMES.
      10 EDU-DEGREE PIC X(100).
      10 EDU-UNIVERSITY PIC X(100).
      10 EDU-YEARS PIC X(50).
FD TEMP-PROFILES-FILE.
01 TEMP-PROFILE-RECORD.
   05 TEMP-PROFILE-USERNAME PIC X(20).
   05 TEMP-PROFILE-FIRST-NAME PIC X(50).
   05 TEMP-PROFILE-LAST-NAME PIC X(50).
   05 TEMP-PROFILE-UNIVERSITY PIC X(100).
   05 TEMP-PROFILE-MAJOR PIC X(50).
   05 TEMP-PROFILE-GRAD-YEAR PIC X(4).
   05 TEMP-PROFILE-ABOUT PIC X(200).
   05 TEMP-PROFILE-EXP-COUNT PIC 9.
   05 TEMP-PROFILE-EXPERIENCE OCCURS 3 TIMES.
      10 TEMP-EXP-TITLE PIC X(100).
      10 TEMP-EXP-COMPANY PIC X(100).
      10 TEMP-EXP-DATES PIC X(50).
      10 TEMP-EXP-DESC PIC X(200).
   05 TEMP-PROFILE-EDU-COUNT PIC 9.
   05 TEMP-PROFILE-EDUCATION OCCURS 3 TIMES.
      10 TEMP-EDU-DEGREE PIC X(100).
      10 TEMP-EDU-UNIVERSITY PIC X(100).
      10 TEMP-EDU-YEARS PIC X(50).
FD CONNECTIONS-FILE.
01 CONNECTION-RECORD.
   05 CONN-SENDER-USERNAME PIC X(20).
   05 CONN-RECIPIENT-USERNAME PIC X(20).
   05 CONN-STATUS PIC X(10).
   05 CONN-MESSAGE PIC X(200).
   05 CONN-TIMESTAMP PIC X(19).
FD TEMP-CONNECTIONS-FILE.
01 TEMP-CONNECTION-RECORD.
   05 TEMP-CONN-SENDER-USERNAME PIC X(20).
   05 TEMP-CONN-RECIPIENT-USERNAME PIC X(20).
   05 TEMP-CONN-STATUS PIC X(10).
   05 TEMP-CONN-MESSAGE PIC X(200).
   05 TEMP-CONN-TIMESTAMP PIC X(19).
WORKING-STORAGE SECTION.
01 WS-FLAGS.
   05 WS-EOF-FLAG PIC X VALUE 'N'.
      88 WS-END-OF-FILE VALUE 'Y'.
   05 WS-VALID-PASSWORD PIC X VALUE 'N'.
   05 WS-VALID-GRAD-YEAR PIC X VALUE 'N'.
   05 WS-ACCOUNT-EXISTS PIC X VALUE 'N'.
   05 WS-LOGGED-IN PIC X VALUE 'N'.
   05 WS-EXIT-FLAG PIC X VALUE 'N'.
   05 WS-PROFILE-FOUND PIC X VALUE 'N'.
   05 WS-PROFILE-UPDATED PIC X VALUE 'N'.
   05 WS-VIEW-SEARCH PIC X VALUE 'N'.
      88 VIEWING-SEARCH VALUE 'Y'.
      88 VIEWING-OWN    VALUE 'N'.
   05 WS-CONNECTION-FOUND PIC X VALUE 'N'.
   05 WS-CONN-REQUEST-EXISTS PIC X VALUE 'N'.
   05 WS-CONN-ALREADY-CONNECTED PIC X VALUE 'N'.
01 WS-COUNTERS.
   05 WS-ACCOUNT-COUNT PIC 9(2) VALUE 0.
   05 WS-CHAR-COUNT PIC 9(2) VALUE 0.
   05 WS-UPPER-COUNT PIC 9(2) VALUE 0.
   05 WS-DIGIT-COUNT PIC 9(2) VALUE 0.
   05 WS-SPECIAL-COUNT PIC 9(2) VALUE 0.
   05 WS-EXP-COUNT PIC 9 VALUE 0.
   05 WS-EDU-COUNT PIC 9 VALUE 0.
   05 WS-EXP-IDX PIC 9.
   05 WS-EDU-IDX PIC 9.
   05 WS-INDEX PIC 9(2).
01 WS-INPUT-DATA.
   05 WS-MENU-CHOICE PIC X(2).
   05 WS-USERNAME PIC X(20).
   05 WS-PASSWORD PIC X(12).
   05 WS-SKILL-CHOICE PIC X(2).
   05 WS-CONN-CHOICE PIC X(2).
   05 WS-TARGET-USERNAME PIC X(20).
   05 WS-CONNECTION-MESSAGE PIC X(200).
01 WS-PROFILE-DATA.
   05 WS-FIRST-NAME PIC X(50).
   05 WS-LAST-NAME PIC X(50).
   05 WS-UNIVERSITY PIC X(100).
   05 WS-MAJOR PIC X(50).
   05 WS-GRAD-YEAR PIC X(4).
   05 WS-ABOUT PIC X(200).
   05 WS-EXPERIENCE-ENTRIES OCCURS 3 TIMES.
      10 WS-EXP-TITLE PIC X(100).
      10 WS-EXP-COMPANY PIC X(100).
      10 WS-EXP-DATES PIC X(50).
      10 WS-EXP-DESC PIC X(200).
   05 WS-EDUCATION-ENTRIES OCCURS 3 TIMES.
      10 WS-EDU-DEGREE PIC X(100).
      10 WS-EDU-UNIVERSITY PIC X(100).
      10 WS-EDU-YEARS PIC X(50).
01 WS-TEMP-VARS.
   05 WS-CHAR PIC X.
   05 WS-PASSWORD-LENGTH PIC 9(2).
   05 WS-HASHED-PASSWORD PIC X(12).
   05 WS-REVERSE-INDEX PIC 9(2).
   05 WS-WELCOME-PERSONAL PIC X(50).
   05 WS-TRIMMED-USERNAME PIC X(20).
   05 WS-TRIMMED-PASSWORD PIC X(12).
   05 WS-TEMP-INPUT PIC X(200).
   05 WS-SEARCH-NAME PIC X(100).
   05 WS-FULL-NAME PIC X(100).
01 WS-MESSAGES.
   05 WS-WELCOME-MSG PIC X(30) VALUE "Welcome to InCollege!".
   05 WS-MENU-OPT1 PIC X(20) VALUE "Log In".
   05 WS-MENU-OPT2 PIC X(25) VALUE "Create New Account".
   05 WS-CHOICE-PROMPT PIC X(20) VALUE "Enter your choice: ".
   05 WS-USERNAME-PROMPT PIC X(25) VALUE "Please enter username: ".
   05 WS-PASSWORD-PROMPT PIC X(25) VALUE "Please enter password: ".
   05 WS-SUCCESS-MSG PIC X(40) VALUE "Account successfully created!".
   05 WS-LIMIT-MSG PIC X(80) VALUE "All permitted accounts have been created, please come back later".
   05 WS-EXISTS-MSG PIC X(30) VALUE "Username already exists!".
   05 WS-INVALID-PASS-MSG PIC X(40) VALUE "Password does not meet requirements!".
   05 WS-PASS-REQ-MSG1 PIC X(40) VALUE "Password must be 8-12 characters,".
   05 WS-PASS-REQ-MSG2 PIC X(40) VALUE "contain 1 uppercase, 1 digit,".
   05 WS-PASS-REQ-MSG3 PIC X(30) VALUE "and 1 special character.".
   05 WS-LOGIN-SUCCESS PIC X(32) VALUE "You have successfully logged in.".
   05 WS-LOGIN-FAIL PIC X(46) VALUE "Incorrect username/password, please try again".
   05 WS-UNDER-JOB PIC X(45) VALUE "Job search/internship is under construction.".
   05 WS-UNDER-FIND PIC X(45) VALUE "Find someone you know is under construction.".
   05 WS-UNDER-SKILL PIC X(35) VALUE "This skill is under construction.".
   05 WS-JOB-SEARCH PIC X(30) VALUE "Search for a job".
   05 WS-FIND-SOMEONE PIC X(30) VALUE "Find someone that you know".
   05 WS-LEARN-SKILL PIC X(30) VALUE "Learn a new skill".
   05 WS-SKILL-TITLE PIC X(20) VALUE "Learn a New Skill:".
   05 WS-SKILL1 PIC X(20) VALUE "Skill 1".
   05 WS-SKILL2 PIC X(20) VALUE "Skill 2".
   05 WS-SKILL3 PIC X(20) VALUE "Skill 3".
   05 WS-SKILL4 PIC X(20) VALUE "Skill 4".
   05 WS-SKILL5 PIC X(20) VALUE "Skill 5".
   05 WS-SKILL6 PIC X(15) VALUE "Go Back".
   05 WS-INVALID-CHOICE PIC X(15) VALUE "Invalid choice".
   05 WS-WELCOME-PREFIX PIC X(9) VALUE "Welcome, ".
   05 WS-WELCOME-SUFFIX PIC X(1) VALUE "!".
   05 WS-ERROR-MSG PIC X(50) VALUE "Error: Invalid account record format".
   05 WS-PROFILE-CREATE PIC X(25) VALUE "Create/Edit My Profile".
   05 WS-PROFILE-VIEW PIC X(20) VALUE "View My Profile".
   05 WS-PROFILE-HEADER PIC X(20) VALUE "Create/Edit Profile".
   05 WS-FIRST-PROMPT PIC X(20) VALUE "Enter First Name: ".
   05 WS-LAST-PROMPT PIC X(19) VALUE "Enter Last Name: ".
   05 WS-UNIV-PROMPT PIC X(35) VALUE "Enter University/College Attended: ".
   05 WS-MAJOR-PROMPT PIC X(15) VALUE "Enter Major: ".
   05 WS-GRAD-PROMPT PIC X(30) VALUE "Enter Graduation Year (YYYY): ".
   05 WS-ABOUT-PROMPT PIC X(69) VALUE "Enter About Me (optional, max 200 chars, enter blank line to skip): ".
   05 WS-EXP-PROMPT PIC X(70) VALUE "Add Experience (optional, max 3 entries. Enter 'DONE' to finish): ".
   05 WS-EDU-PROMPT PIC X(70) VALUE "Add Education (optional, max 3 entries. Enter 'DONE' to finish): ".
   05 WS-PROFILE-SAVED PIC X(27) VALUE "Profile saved successfully!".
   05 WS-INVALID-YEAR PIC X(60) VALUE "Invalid year. Must be a 4-digit number between 1900-2100.".
   05 WS-VIEW-HEADER PIC X(15) VALUE "Your Profile".
   05 WS-NAME-LABEL PIC X(8) VALUE "Name: ".
   05 WS-UNIV-LABEL PIC X(12) VALUE "University: ".
   05 WS-MAJOR-LABEL PIC X(7) VALUE "Major: ".
   05 WS-GRAD-LABEL PIC X(17) VALUE "Graduation Year: ".
   05 WS-ABOUT-LABEL PIC X(10) VALUE "About Me: ".
   05 WS-EXP-LABEL PIC X(12) VALUE "Experience:".
   05 WS-TITLE-LABEL PIC X(9) VALUE " Title: ".
   05 WS-COMP-LABEL PIC X(11) VALUE " Company: ".
   05 WS-DATES-LABEL PIC X(9) VALUE " Dates: ".
   05 WS-DESC-LABEL PIC X(15) VALUE " Description: ".
   05 WS-EDU-LABEL PIC X(11) VALUE "Education:".
   05 WS-DEGREE-LABEL PIC X(10) VALUE " Degree: ".
   05 WS-EDU-UNIV-LABEL PIC X(14) VALUE " University: ".
   05 WS-YEARS-LABEL PIC X(9) VALUE " Years: ".
   05 WS-NO-PROFILE PIC X(20) VALUE "No profile found.".
   05 WS-CONN-MENU-OPT1 PIC X(30) VALUE "Send Connection Request".
   05 WS-CONN-MENU-OPT2 PIC X(30) VALUE "View Pending Requests".
   05 WS-CONN-MENU-OPT3 PIC X(30) VALUE "Manage Connection Requests".
   05 WS-CONN-MENU-OPT4 PIC X(20) VALUE "My Connections".
   05 WS-CONN-SENT-MSG PIC X(40) VALUE "Connection request sent successfully!".
   05 WS-CONN-EXISTS-MSG PIC X(45) VALUE "You are already connected with this user!".
   05 WS-CONN-PENDING-MSG PIC X(70) VALUE "You already have a pending connection request with this user!".
   05 WS-CONN-SELF-MSG PIC X(45) VALUE "Cannot send connection request to yourself!".
   05 WS-CONN-USER-NOT-FOUND PIC X(25) VALUE "User not found!".
   05 WS-CONN-ACCEPTED-MSG PIC X(30) VALUE "Connection request accepted!".
   05 WS-CONN-REJECTED-MSG PIC X(30) VALUE "Connection request rejected!".
   05 WS-NO-PENDING-MSG PIC X(55) VALUE "You have no pending connection requests at this time.".
   05 WS-NO-CONNECTIONS-MSG PIC X(30) VALUE "You have no connections yet.".
   05 WS-CONN-USERNAME-PROMPT PIC X(40) VALUE "Enter username to connect with: ".
   05 WS-CONN-MESSAGE-PROMPT PIC X(60) VALUE "Enter message (optional, max 200 chars, blank to skip): ".
01 WS-NAME-LOOKUP-VARS.
   05 WS-LOOKUP-USERNAME PIC X(20).
   05 WS-LOOKUP-FIRST-NAME PIC X(50).
   05 WS-LOOKUP-LAST-NAME PIC X(50).
   05 WS-LOOKUP-FULL-NAME PIC X(101).
   05 WS-LOOKUP-UNIVERSITY PIC X(100).
   05 WS-LOOKUP-MAJOR PIC X(50).
   05 WS-USER-FOUND PIC X VALUE 'N'.
01 WS-STORED-ACCOUNTS.
   05 WS-ACCOUNT OCCURS 5 TIMES.
      10 WS-STORED-USERNAME PIC X(20).
      10 WS-STORED-PASSWORD PIC X(12).
      10 WS-STORED-PASSWORD-LENGTH PIC 9(2).
01 ACCOUNTS-STATUS PIC XX.
01 PROFILES-STATUS PIC XX.
01 TEMP-PROFILES-STATUS PIC XX.
01 CONNECTIONS-STATUS PIC XX.
01 TEMP-CONNECTIONS-STATUS PIC XX.
PROCEDURE DIVISION.
MAIN-PROGRAM.
    PERFORM INITIALIZATION
    PERFORM MAIN-LOOP UNTIL WS-EOF-FLAG = 'Y'
    PERFORM CLEANUP
    STOP RUN.
INITIALIZATION.
    OPEN INPUT INPUT-FILE
    OPEN OUTPUT OUTPUT-FILE
    PERFORM INITIALIZE-FILES
    PERFORM LOAD-EXISTING-ACCOUNTS
    PERFORM DISPLAY-WELCOME.
INITIALIZE-FILES.
    OPEN INPUT ACCOUNTS-FILE
    IF ACCOUNTS-STATUS = "35"
        OPEN OUTPUT ACCOUNTS-FILE
        CLOSE ACCOUNTS-FILE
    ELSE
        CLOSE ACCOUNTS-FILE
    END-IF
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        OPEN OUTPUT PROFILES-FILE
        CLOSE PROFILES-FILE
    ELSE
        CLOSE PROFILES-FILE
    END-IF
    OPEN INPUT TEMP-PROFILES-FILE
    IF TEMP-PROFILES-STATUS = "35"
        OPEN OUTPUT TEMP-PROFILES-FILE
        CLOSE TEMP-PROFILES-FILE
    ELSE
        CLOSE TEMP-PROFILES-FILE
    END-IF
    OPEN INPUT CONNECTIONS-FILE
    IF CONNECTIONS-STATUS = "35"
        OPEN OUTPUT CONNECTIONS-FILE
        CLOSE CONNECTIONS-FILE
    ELSE
        CLOSE CONNECTIONS-FILE
    END-IF
    OPEN INPUT TEMP-CONNECTIONS-FILE
    IF TEMP-CONNECTIONS-STATUS = "35"
        OPEN OUTPUT TEMP-CONNECTIONS-FILE
        CLOSE TEMP-CONNECTIONS-FILE
    ELSE
        CLOSE TEMP-CONNECTIONS-FILE
    END-IF.
LOAD-EXISTING-ACCOUNTS.
    OPEN INPUT ACCOUNTS-FILE
    MOVE 0 TO WS-ACCOUNT-COUNT
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-ACCOUNT-COUNT = 5
        READ ACCOUNTS-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF ACCOUNT-PASSWORD-LENGTH >= 08 AND ACCOUNT-PASSWORD-LENGTH <= 12
                    ADD 1 TO WS-ACCOUNT-COUNT
                    MOVE ACCOUNT-USERNAME TO WS-STORED-USERNAME(WS-ACCOUNT-COUNT)
                    MOVE ACCOUNT-PASSWORD TO WS-STORED-PASSWORD(WS-ACCOUNT-COUNT)
                    MOVE ACCOUNT-PASSWORD-LENGTH TO WS-STORED-PASSWORD-LENGTH(WS-ACCOUNT-COUNT)
                ELSE
                    MOVE WS-ERROR-MSG TO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY WS-ERROR-MSG
                END-IF
        END-READ
    END-PERFORM
    CLOSE ACCOUNTS-FILE
    MOVE 'N' TO WS-EOF-FLAG.
DISPLAY-WELCOME.
    MOVE WS-WELCOME-MSG TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-WELCOME-MSG
    MOVE "1. " TO OUTPUT-RECORD(1:3)
    MOVE WS-MENU-OPT1 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "1. " WS-MENU-OPT1
    MOVE "2. " TO OUTPUT-RECORD(1:3)
    MOVE WS-MENU-OPT2 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "2. " WS-MENU-OPT2
    MOVE WS-CHOICE-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-CHOICE-PROMPT.
MAIN-LOOP.
    READ INPUT-FILE INTO WS-MENU-CHOICE
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
        NOT AT END
            EVALUATE TRUE
                WHEN WS-MENU-CHOICE = "1" OR WS-MENU-CHOICE = "1 "
                    PERFORM LOGIN-PROCESS
                WHEN WS-MENU-CHOICE = "2" OR WS-MENU-CHOICE = "2 "
                    PERFORM CREATE-ACCOUNT-PROCESS
                WHEN OTHER
                    MOVE WS-INVALID-CHOICE TO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY WS-INVALID-CHOICE
                    PERFORM DISPLAY-WELCOME
            END-EVALUATE
    END-READ.
CREATE-ACCOUNT-PROCESS.
    IF WS-ACCOUNT-COUNT >= 5
        MOVE WS-LIMIT-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-LIMIT-MSG
    ELSE
        PERFORM GET-USERNAME
        IF WS-EOF-FLAG NOT = 'Y'
            PERFORM GET-PASSWORD
            IF WS-EOF-FLAG NOT = 'Y' AND
               WS-ACCOUNT-EXISTS = 'N' AND
               WS-VALID-PASSWORD = 'Y'
                PERFORM HASH-PASSWORD
                PERFORM SAVE-ACCOUNT
            END-IF
        END-IF
    END-IF
    PERFORM DISPLAY-WELCOME.
GET-USERNAME.
    MOVE WS-USERNAME-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-USERNAME-PROMPT
    READ INPUT-FILE INTO WS-USERNAME
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
        NOT AT END
            MOVE FUNCTION TRIM(WS-USERNAME) TO WS-TRIMMED-USERNAME
            MOVE WS-TRIMMED-USERNAME TO WS-USERNAME
            PERFORM CHECK-USERNAME-EXISTS
    END-READ.
CHECK-USERNAME-EXISTS.
    MOVE 'N' TO WS-ACCOUNT-EXISTS
    MOVE 1 TO WS-INDEX
    PERFORM UNTIL WS-INDEX > WS-ACCOUNT-COUNT
               OR WS-ACCOUNT-EXISTS = 'Y'
        IF FUNCTION TRIM(WS-USERNAME) = FUNCTION TRIM(WS-STORED-USERNAME(WS-INDEX))
            MOVE 'Y' TO WS-ACCOUNT-EXISTS
            MOVE WS-EXISTS-MSG TO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-EXISTS-MSG
        END-IF
        ADD 1 TO WS-INDEX
    END-PERFORM.
GET-PASSWORD.
    IF WS-ACCOUNT-EXISTS = 'N'
        MOVE WS-PASSWORD-PROMPT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-PASSWORD-PROMPT
        READ INPUT-FILE INTO WS-PASSWORD
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                MOVE FUNCTION TRIM(WS-PASSWORD) TO WS-TRIMMED-PASSWORD
                MOVE WS-TRIMMED-PASSWORD TO WS-PASSWORD
                PERFORM VALIDATE-PASSWORD
        END-READ
    END-IF.
VALIDATE-PASSWORD.
    MOVE 0 TO WS-CHAR-COUNT
    MOVE 0 TO WS-UPPER-COUNT
    MOVE 0 TO WS-DIGIT-COUNT
    MOVE 0 TO WS-SPECIAL-COUNT
    MOVE FUNCTION TRIM(WS-PASSWORD) TO WS-TRIMMED-PASSWORD
    INSPECT FUNCTION REVERSE(WS-TRIMMED-PASSWORD)
      TALLYING WS-CHAR-COUNT FOR LEADING SPACES
    COMPUTE WS-CHAR-COUNT =
      FUNCTION LENGTH(WS-TRIMMED-PASSWORD) - WS-CHAR-COUNT
    MOVE 1 TO WS-INDEX
    PERFORM UNTIL WS-INDEX > WS-CHAR-COUNT
        MOVE WS-TRIMMED-PASSWORD(WS-INDEX:1) TO WS-CHAR
        IF WS-CHAR >= 'A' AND WS-CHAR <= 'Z'
            ADD 1 TO WS-UPPER-COUNT
        END-IF
        IF WS-CHAR >= '0' AND WS-CHAR <= '9'
            ADD 1 TO WS-DIGIT-COUNT
        END-IF
        IF (WS-CHAR >= '!' AND WS-CHAR <= '/') OR
           (WS-CHAR >= ':' AND WS-CHAR <= '@') OR
           (WS-CHAR >= '[' AND WS-CHAR <= '`') OR
           (WS-CHAR >= '{' AND WS-CHAR <= '~')
            ADD 1 TO WS-SPECIAL-COUNT
        END-IF
        ADD 1 TO WS-INDEX
    END-PERFORM
    IF WS-CHAR-COUNT >= 8 AND WS-CHAR-COUNT <= 12 AND
       WS-UPPER-COUNT >= 1 AND WS-DIGIT-COUNT >= 1 AND
       WS-SPECIAL-COUNT >= 1
        MOVE 'Y' TO WS-VALID-PASSWORD
    ELSE
        MOVE 'N' TO WS-VALID-PASSWORD
        MOVE WS-INVALID-PASS-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-INVALID-PASS-MSG
        MOVE WS-PASS-REQ-MSG1 TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-PASS-REQ-MSG1
        MOVE WS-PASS-REQ-MSG2 TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-PASS-REQ-MSG2
        MOVE WS-PASS-REQ-MSG3 TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-PASS-REQ-MSG3
    END-IF.
HASH-PASSWORD.
    MOVE 0 TO WS-CHAR-COUNT
    MOVE FUNCTION TRIM(WS-PASSWORD) TO WS-TRIMMED-PASSWORD
    INSPECT FUNCTION REVERSE(WS-TRIMMED-PASSWORD)
      TALLYING WS-CHAR-COUNT FOR LEADING SPACES
    COMPUTE WS-PASSWORD-LENGTH =
      FUNCTION LENGTH(WS-TRIMMED-PASSWORD) - WS-CHAR-COUNT
    MOVE SPACES TO WS-HASHED-PASSWORD
    MOVE WS-PASSWORD-LENGTH TO WS-REVERSE-INDEX
    MOVE 1 TO WS-INDEX
    PERFORM UNTIL WS-INDEX > WS-PASSWORD-LENGTH
        MOVE WS-TRIMMED-PASSWORD(WS-INDEX:1)
          TO WS-HASHED-PASSWORD(WS-REVERSE-INDEX:1)
        SUBTRACT 1 FROM WS-REVERSE-INDEX
        ADD 1 TO WS-INDEX
    END-PERFORM.
SAVE-ACCOUNT.
    ADD 1 TO WS-ACCOUNT-COUNT
    MOVE WS-USERNAME TO WS-STORED-USERNAME(WS-ACCOUNT-COUNT)
    MOVE WS-HASHED-PASSWORD TO WS-STORED-PASSWORD(WS-ACCOUNT-COUNT)
    MOVE WS-PASSWORD-LENGTH TO WS-STORED-PASSWORD-LENGTH(WS-ACCOUNT-COUNT)
    OPEN EXTEND ACCOUNTS-FILE
    MOVE SPACES TO ACCOUNT-RECORD
    STRING FUNCTION TRIM(WS-USERNAME) DELIMITED BY SIZE
           INTO ACCOUNT-USERNAME
    STRING WS-HASHED-PASSWORD DELIMITED BY SIZE
           INTO ACCOUNT-PASSWORD
    MOVE WS-PASSWORD-LENGTH TO ACCOUNT-PASSWORD-LENGTH
    WRITE ACCOUNT-RECORD
    CLOSE ACCOUNTS-FILE
    MOVE WS-SUCCESS-MSG TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-SUCCESS-MSG.
LOGIN-PROCESS.
    MOVE 'N' TO WS-LOGGED-IN
    PERFORM UNTIL WS-LOGGED-IN = 'Y' OR WS-EOF-FLAG = 'Y'
        MOVE WS-USERNAME-PROMPT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-USERNAME-PROMPT
        READ INPUT-FILE INTO WS-USERNAME
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                MOVE FUNCTION TRIM(WS-USERNAME) TO WS-TRIMMED-USERNAME
                MOVE WS-TRIMMED-USERNAME TO WS-USERNAME
        END-READ
        IF WS-EOF-FLAG = 'Y'
            EXIT PERFORM
        END-IF
        MOVE WS-PASSWORD-PROMPT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-PASSWORD-PROMPT
        READ INPUT-FILE INTO WS-PASSWORD
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                MOVE FUNCTION TRIM(WS-PASSWORD) TO WS-TRIMMED-PASSWORD
                MOVE WS-TRIMMED-PASSWORD TO WS-PASSWORD
        END-READ
        IF WS-EOF-FLAG = 'Y'
            EXIT PERFORM
        END-IF
        PERFORM CHECK-CREDENTIALS
        IF WS-LOGGED-IN = 'N'
            MOVE WS-LOGIN-FAIL TO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-LOGIN-FAIL
            PERFORM DISPLAY-WELCOME
            MOVE 'N' TO WS-EOF-FLAG
        END-IF
    END-PERFORM
    IF WS-LOGGED-IN = 'Y'
        MOVE WS-LOGIN-SUCCESS TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-LOGIN-SUCCESS

        MOVE SPACES TO WS-WELCOME-PERSONAL

        STRING WS-WELCOME-PREFIX DELIMITED BY SIZE
               FUNCTION TRIM(WS-USERNAME) DELIMITED BY SPACE
               WS-WELCOME-SUFFIX DELIMITED BY SIZE
               INTO WS-WELCOME-PERSONAL
        MOVE WS-WELCOME-PERSONAL TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-WELCOME-PERSONAL

        PERFORM POST-LOGIN-MENU
    END-IF.
CHECK-CREDENTIALS.
    MOVE 'N' TO WS-LOGGED-IN
    PERFORM HASH-PASSWORD
    MOVE 1 TO WS-INDEX
    PERFORM UNTIL WS-INDEX > WS-ACCOUNT-COUNT OR WS-LOGGED-IN = 'Y'
        IF FUNCTION TRIM(WS-USERNAME) = FUNCTION TRIM(WS-STORED-USERNAME(WS-INDEX)) AND
           WS-HASHED-PASSWORD = WS-STORED-PASSWORD(WS-INDEX) AND
           WS-PASSWORD-LENGTH = WS-STORED-PASSWORD-LENGTH(WS-INDEX)
            MOVE 'Y' TO WS-LOGGED-IN
        END-IF
        ADD 1 TO WS-INDEX
    END-PERFORM
    IF WS-LOGGED-IN = 'N'
        MOVE WS-LOGIN-FAIL TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-LOGIN-FAIL
    END-IF.
POST-LOGIN-MENU.
    MOVE 'N' TO WS-EXIT-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-EXIT-FLAG = 'Y'
        PERFORM DISPLAY-POST-LOGIN-OPTIONS
        READ INPUT-FILE INTO WS-MENU-CHOICE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                EVALUATE WS-MENU-CHOICE
                    WHEN "1"
                        PERFORM CREATE-EDIT-PROFILE
                    WHEN "2"
                        PERFORM LOAD-PROFILE
                        IF WS-PROFILE-FOUND = 'Y'
                            PERFORM VIEW-PROFILE
                        ELSE
                            MOVE "No profile found. Please create a profile first." TO OUTPUT-RECORD
                            WRITE OUTPUT-RECORD
                            DISPLAY "No profile found. Please create a profile first."
                        END-IF
                    WHEN "3"
                        PERFORM SEARCH-FOR-USER-WITH-CONNECT
                    WHEN "4"
                        PERFORM LEARN-SKILL-MENU
                    WHEN "5"
                        PERFORM VIEW-MY-PENDING-REQUESTS
                    WHEN "6"
                        PERFORM VIEW-MY-NETWORK
                    WHEN OTHER
                        MOVE WS-INVALID-CHOICE TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-INVALID-CHOICE
                END-EVALUATE
                MOVE 'N' TO WS-EOF-FLAG
        END-READ
    END-PERFORM.
DISPLAY-POST-LOGIN-OPTIONS.
    MOVE "1. Create/Edit Profile" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "1. Create/Edit Profile"
    MOVE "2. View My Profile" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "2. View My Profile"
    MOVE "3. Search for User" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "3. Search for User"
    MOVE "4. Learn a New Skill" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "4. Learn a New Skill"
    MOVE "5. View My Pending Connection Requests" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "5. View My Pending Connection Requests"
    MOVE "6. View My Network" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "6. View My Network"
    MOVE WS-CHOICE-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-CHOICE-PROMPT.
CREATE-EDIT-PROFILE.
    MOVE WS-PROFILE-HEADER TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-PROFILE-HEADER
    MOVE WS-FIRST-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-FIRST-PROMPT
    READ INPUT-FILE INTO WS-FIRST-NAME
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ
    MOVE WS-LAST-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-LAST-PROMPT
    READ INPUT-FILE INTO WS-LAST-NAME
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ
    MOVE WS-UNIV-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-UNIV-PROMPT
    READ INPUT-FILE INTO WS-UNIVERSITY
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ
    MOVE WS-MAJOR-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-MAJOR-PROMPT
    READ INPUT-FILE INTO WS-MAJOR
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ
    MOVE WS-GRAD-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-GRAD-PROMPT
    READ INPUT-FILE INTO WS-GRAD-YEAR
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ
    MOVE FUNCTION TRIM(WS-GRAD-YEAR) TO WS-GRAD-YEAR
    PERFORM VALIDATE-GRAD-YEAR
    IF WS-VALID-GRAD-YEAR = 'N' EXIT PARAGRAPH END-IF
    MOVE WS-ABOUT-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-ABOUT-PROMPT
    READ INPUT-FILE INTO WS-ABOUT
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ
    PERFORM GET-EXPERIENCE
    PERFORM GET-EDUCATION
    PERFORM SAVE-PROFILE
    MOVE WS-PROFILE-SAVED TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-PROFILE-SAVED.
GET-EXPERIENCE.
    MOVE 0 TO WS-EXP-COUNT
    MOVE 1 TO WS-EXP-IDX
    PERFORM UNTIL WS-EXP-IDX > 3 OR WS-EOF-FLAG = 'Y'
        MOVE WS-EXP-PROMPT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EXP-PROMPT
        READ INPUT-FILE INTO WS-TEMP-INPUT
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        IF FUNCTION UPPER-CASE(FUNCTION TRIM(WS-TEMP-INPUT)) = "DONE"
            EXIT PERFORM
        END-IF
        MOVE WS-TEMP-INPUT TO WS-EXP-TITLE(WS-EXP-IDX)
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Experience #" WS-EXP-IDX " - Title:"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Experience #" WS-EXP-IDX " - Title:"
        MOVE WS-TEMP-INPUT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-TEMP-INPUT
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Experience #" WS-EXP-IDX " - Company/Organization:"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Experience #" WS-EXP-IDX " - Company/Organization:"
        READ INPUT-FILE INTO WS-EXP-COMPANY(WS-EXP-IDX)
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        MOVE WS-EXP-COMPANY(WS-EXP-IDX) TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EXP-COMPANY(WS-EXP-IDX)
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Experience #" WS-EXP-IDX " - Dates (e.g., Summer 2024):"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Experience #" WS-EXP-IDX " - Dates (e.g., Summer 2024):"
        READ INPUT-FILE INTO WS-EXP-DATES(WS-EXP-IDX)
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        MOVE WS-EXP-DATES(WS-EXP-IDX) TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EXP-DATES(WS-EXP-IDX)
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Experience #" WS-EXP-IDX " - Description (optional, max 100 chars, blank to skip):"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Experience #" WS-EXP-IDX " - Description (optional, max 100 chars, blank to skip):"
        READ INPUT-FILE INTO WS-EXP-DESC(WS-EXP-IDX)
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        MOVE WS-EXP-DESC(WS-EXP-IDX) TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EXP-DESC(WS-EXP-IDX)
        ADD 1 TO WS-EXP-COUNT
        ADD 1 TO WS-EXP-IDX
    END-PERFORM.
GET-EDUCATION.
    MOVE 0 TO WS-EDU-COUNT
    MOVE 1 TO WS-EDU-IDX
    PERFORM UNTIL WS-EDU-IDX > 3 OR WS-EOF-FLAG = 'Y'
        MOVE WS-EDU-PROMPT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EDU-PROMPT
        READ INPUT-FILE INTO WS-TEMP-INPUT
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        IF FUNCTION UPPER-CASE(FUNCTION TRIM(WS-TEMP-INPUT)) = "DONE"
            EXIT PERFORM
        END-IF
        MOVE WS-TEMP-INPUT TO WS-EDU-DEGREE(WS-EDU-IDX)
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Education #" WS-EDU-IDX " - Degree:"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Education #" WS-EDU-IDX " - Degree:"
        MOVE WS-TEMP-INPUT TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-TEMP-INPUT
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Education #" WS-EDU-IDX " - University/College:"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Education #" WS-EDU-IDX " - University/College:"
        READ INPUT-FILE INTO WS-EDU-UNIVERSITY(WS-EDU-IDX)
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        MOVE WS-EDU-UNIVERSITY(WS-EDU-IDX) TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EDU-UNIVERSITY(WS-EDU-IDX)
        MOVE SPACES TO OUTPUT-RECORD
        STRING "Education #" WS-EDU-IDX " - Years Attended (e.g., 2023-2025):"
            DELIMITED BY SIZE INTO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Education #" WS-EDU-IDX " - Years Attended (e.g., 2023-2025):"
        READ INPUT-FILE INTO WS-EDU-YEARS(WS-EDU-IDX)
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
                EXIT PARAGRAPH
        END-READ
        MOVE WS-EDU-YEARS(WS-EDU-IDX) TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EDU-YEARS(WS-EDU-IDX)
        ADD 1 TO WS-EDU-COUNT
        ADD 1 TO WS-EDU-IDX
    END-PERFORM.
SAVE-PROFILE.
    MOVE 'N' TO WS-PROFILE-UPDATED
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        OPEN OUTPUT PROFILES-FILE
        PERFORM WRITE-NEW-PROFILE-RECORD
        CLOSE PROFILES-FILE
        EXIT PARAGRAPH
    END-IF
    OPEN OUTPUT TEMP-PROFILES-FILE
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y'
        READ PROFILES-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(PROFILE-USERNAME) = FUNCTION TRIM(WS-USERNAME)
                    PERFORM WRITE-NEW-PROFILE-RECORD
                    MOVE 'Y' TO WS-PROFILE-UPDATED
                ELSE
                    MOVE PROFILE-RECORD TO TEMP-PROFILE-RECORD
                    WRITE TEMP-PROFILE-RECORD
                END-IF
        END-READ
    END-PERFORM
    CLOSE PROFILES-FILE
    CLOSE TEMP-PROFILES-FILE
    IF WS-PROFILE-UPDATED = 'N'
        OPEN EXTEND PROFILES-FILE
        PERFORM WRITE-NEW-PROFILE-RECORD
        CLOSE PROFILES-FILE
    ELSE
        OPEN OUTPUT PROFILES-FILE
        OPEN INPUT TEMP-PROFILES-FILE
        MOVE 'N' TO WS-EOF-FLAG
        PERFORM UNTIL WS-EOF-FLAG = 'Y'
            READ TEMP-PROFILES-FILE INTO PROFILE-RECORD
                AT END
                    MOVE 'Y' TO WS-EOF-FLAG
                NOT AT END
                    WRITE PROFILE-RECORD
            END-READ
        END-PERFORM
        CLOSE PROFILES-FILE
        CLOSE TEMP-PROFILES-FILE
    END-IF.
WRITE-NEW-PROFILE-RECORD.
    MOVE FUNCTION TRIM(WS-USERNAME) TO PROFILE-USERNAME
    MOVE WS-FIRST-NAME TO PROFILE-FIRST-NAME
    MOVE WS-LAST-NAME TO PROFILE-LAST-NAME
    MOVE WS-UNIVERSITY TO PROFILE-UNIVERSITY
    MOVE WS-MAJOR TO PROFILE-MAJOR
    MOVE WS-GRAD-YEAR TO PROFILE-GRAD-YEAR
    MOVE WS-ABOUT TO PROFILE-ABOUT
    MOVE WS-EXP-COUNT TO PROFILE-EXP-COUNT
    PERFORM VARYING WS-EXP-IDX FROM 1 BY 1 UNTIL WS-EXP-IDX > 3
        MOVE WS-EXP-TITLE(WS-EXP-IDX) TO EXP-TITLE(WS-EXP-IDX)
        MOVE WS-EXP-COMPANY(WS-EXP-IDX) TO EXP-COMPANY(WS-EXP-IDX)
        MOVE WS-EXP-DATES(WS-EXP-IDX) TO EXP-DATES(WS-EXP-IDX)
        MOVE WS-EXP-DESC(WS-EXP-IDX) TO EXP-DESC(WS-EXP-IDX)
    END-PERFORM
    MOVE WS-EDU-COUNT TO PROFILE-EDU-COUNT
    PERFORM VARYING WS-EDU-IDX FROM 1 BY 1 UNTIL WS-EDU-IDX > 3
        MOVE WS-EDU-DEGREE(WS-EDU-IDX) TO EDU-DEGREE(WS-EDU-IDX)
        MOVE WS-EDU-UNIVERSITY(WS-EDU-IDX) TO EDU-UNIVERSITY(WS-EDU-IDX)
        MOVE WS-EDU-YEARS(WS-EDU-IDX) TO EDU-YEARS(WS-EDU-IDX)
    END-PERFORM
    WRITE PROFILE-RECORD.
LOAD-PROFILE.
    MOVE 'N' TO WS-PROFILE-FOUND
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        CLOSE PROFILES-FILE
        EXIT PARAGRAPH
    END-IF
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-PROFILE-FOUND = 'Y'
        READ PROFILES-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(PROFILE-USERNAME) = FUNCTION TRIM(WS-USERNAME)
                    MOVE 'Y' TO WS-PROFILE-FOUND
                    MOVE PROFILE-FIRST-NAME TO WS-FIRST-NAME
                    MOVE PROFILE-LAST-NAME TO WS-LAST-NAME
                    MOVE PROFILE-UNIVERSITY TO WS-UNIVERSITY
                    MOVE PROFILE-MAJOR TO WS-MAJOR
                    MOVE PROFILE-GRAD-YEAR TO WS-GRAD-YEAR
                    MOVE PROFILE-ABOUT TO WS-ABOUT
                    MOVE PROFILE-EXP-COUNT TO WS-EXP-COUNT
                    PERFORM VARYING WS-EXP-IDX FROM 1 BY 1 UNTIL WS-EXP-IDX > 3
                        MOVE EXP-TITLE(WS-EXP-IDX) TO WS-EXP-TITLE(WS-EXP-IDX)
                        MOVE EXP-COMPANY(WS-EXP-IDX) TO WS-EXP-COMPANY(WS-EXP-IDX)
                        MOVE EXP-DATES(WS-EXP-IDX) TO WS-EXP-DATES(WS-EXP-IDX)
                        MOVE EXP-DESC(WS-EXP-IDX) TO WS-EXP-DESC(WS-EXP-IDX)
                    END-PERFORM
                    MOVE PROFILE-EDU-COUNT TO WS-EDU-COUNT
                    PERFORM VARYING WS-EDU-IDX FROM 1 BY 1 UNTIL WS-EDU-IDX > 3
                        MOVE EDU-DEGREE(WS-EDU-IDX) TO WS-EDU-DEGREE(WS-EDU-IDX)
                        MOVE EDU-UNIVERSITY(WS-EDU-IDX) TO WS-EDU-UNIVERSITY(WS-EDU-IDX)
                        MOVE EDU-YEARS(WS-EDU-IDX) TO WS-EDU-YEARS(WS-EDU-IDX)
                    END-PERFORM
                END-IF
        END-READ
    END-PERFORM
    CLOSE PROFILES-FILE.
VIEW-PROFILE.
    MOVE "------------------------------" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "------------------------------"
    IF VIEWING-SEARCH
        MOVE "--- Found User Profile ---" TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "--- Found User Profile ---"
    ELSE
        MOVE WS-VIEW-HEADER TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-VIEW-HEADER
    END-IF

    MOVE SPACES TO OUTPUT-RECORD
    STRING WS-NAME-LABEL FUNCTION TRIM(WS-FIRST-NAME) " " FUNCTION TRIM(WS-LAST-NAME)
        DELIMITED BY SIZE INTO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-NAME-LABEL FUNCTION TRIM(WS-FIRST-NAME) " " FUNCTION TRIM(WS-LAST-NAME)

    MOVE SPACES TO OUTPUT-RECORD
    STRING WS-UNIV-LABEL FUNCTION TRIM(WS-UNIVERSITY)
        DELIMITED BY SIZE INTO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-UNIV-LABEL FUNCTION TRIM(WS-UNIVERSITY)

    MOVE SPACES TO OUTPUT-RECORD
    STRING WS-MAJOR-LABEL FUNCTION TRIM(WS-MAJOR)
        DELIMITED BY SIZE INTO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-MAJOR-LABEL FUNCTION TRIM(WS-MAJOR)

    MOVE SPACES TO OUTPUT-RECORD
    STRING WS-GRAD-LABEL WS-GRAD-YEAR
        DELIMITED BY SIZE INTO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-GRAD-LABEL WS-GRAD-YEAR

    MOVE SPACES TO OUTPUT-RECORD
    STRING WS-ABOUT-LABEL FUNCTION TRIM(WS-ABOUT)
        DELIMITED BY SIZE INTO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-ABOUT-LABEL FUNCTION TRIM(WS-ABOUT)

    IF WS-EXP-COUNT > 0
        MOVE WS-EXP-LABEL TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EXP-LABEL
        PERFORM VARYING WS-EXP-IDX FROM 1 BY 1 UNTIL WS-EXP-IDX > WS-EXP-COUNT
            MOVE SPACES TO OUTPUT-RECORD
            STRING WS-TITLE-LABEL FUNCTION TRIM(WS-EXP-TITLE(WS-EXP-IDX))
                DELIMITED BY SIZE INTO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-TITLE-LABEL FUNCTION TRIM(WS-EXP-TITLE(WS-EXP-IDX))

            MOVE SPACES TO OUTPUT-RECORD
            STRING WS-COMP-LABEL FUNCTION TRIM(WS-EXP-COMPANY(WS-EXP-IDX))
                DELIMITED BY SIZE INTO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-COMP-LABEL FUNCTION TRIM(WS-EXP-COMPANY(WS-EXP-IDX))

            MOVE SPACES TO OUTPUT-RECORD
            STRING WS-DATES-LABEL FUNCTION TRIM(WS-EXP-DATES(WS-EXP-IDX))
                DELIMITED BY SIZE INTO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-DATES-LABEL FUNCTION TRIM(WS-EXP-DATES(WS-EXP-IDX))

            IF FUNCTION TRIM(WS-EXP-DESC(WS-EXP-IDX)) NOT = SPACES
                MOVE SPACES TO OUTPUT-RECORD
                STRING WS-DESC-LABEL FUNCTION TRIM(WS-EXP-DESC(WS-EXP-IDX))
                    DELIMITED BY SIZE INTO OUTPUT-RECORD
                WRITE OUTPUT-RECORD
                DISPLAY WS-DESC-LABEL FUNCTION TRIM(WS-EXP-DESC(WS-EXP-IDX))
            END-IF
        END-PERFORM
    ELSE
        MOVE "Experience: None" TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Experience: None"
    END-IF

    IF WS-EDU-COUNT > 0
        MOVE WS-EDU-LABEL TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-EDU-LABEL
        PERFORM VARYING WS-EDU-IDX FROM 1 BY 1 UNTIL WS-EDU-IDX > WS-EDU-COUNT
            MOVE SPACES TO OUTPUT-RECORD
            STRING WS-DEGREE-LABEL FUNCTION TRIM(WS-EDU-DEGREE(WS-EDU-IDX))
                DELIMITED BY SIZE INTO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-DEGREE-LABEL FUNCTION TRIM(WS-EDU-DEGREE(WS-EDU-IDX))

            MOVE SPACES TO OUTPUT-RECORD
            STRING WS-EDU-UNIV-LABEL FUNCTION TRIM(WS-EDU-UNIVERSITY(WS-EDU-IDX))
                DELIMITED BY SIZE INTO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-EDU-UNIV-LABEL FUNCTION TRIM(WS-EDU-UNIVERSITY(WS-EDU-IDX))

            MOVE SPACES TO OUTPUT-RECORD
            STRING WS-YEARS-LABEL FUNCTION TRIM(WS-EDU-YEARS(WS-EDU-IDX))
                DELIMITED BY SIZE INTO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-YEARS-LABEL FUNCTION TRIM(WS-EDU-YEARS(WS-EDU-IDX))
        END-PERFORM
    ELSE
        MOVE "Education: None" TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Education: None"
    END-IF.

    MOVE "-------------------------" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "-------------------------".
VALIDATE-GRAD-YEAR.
    MOVE 'Y' TO WS-VALID-GRAD-YEAR
    IF FUNCTION LENGTH(FUNCTION TRIM(WS-GRAD-YEAR)) NOT = 4
        MOVE "Graduation year must be 4 digits." TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Graduation year must be 4 digits."
        MOVE 'N' TO WS-VALID-GRAD-YEAR
        EXIT PARAGRAPH
    END-IF
    MOVE 1 TO WS-INDEX
    PERFORM UNTIL WS-INDEX > 4
        MOVE WS-GRAD-YEAR(WS-INDEX:1) TO WS-CHAR
        IF WS-CHAR < '0' OR WS-CHAR > '9'
            MOVE "Graduation year must be numeric." TO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY "Graduation year must be numeric."
            MOVE 'N' TO WS-VALID-GRAD-YEAR
            EXIT PARAGRAPH
        END-IF
        ADD 1 TO WS-INDEX
    END-PERFORM
    IF WS-GRAD-YEAR < "1900" OR WS-GRAD-YEAR > "2090"
        MOVE "Graduation year must be between 1900 and 2090." TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "Graduation year must be between 1900 and 2090."
        MOVE 'N' TO WS-VALID-GRAD-YEAR
    END-IF.
LEARN-SKILL-MENU.
    MOVE 'N' TO WS-EXIT-FLAG
    PERFORM UNTIL WS-EXIT-FLAG = 'Y' OR WS-EOF-FLAG = 'Y'
        PERFORM DISPLAY-SKILLS
        READ INPUT-FILE INTO WS-SKILL-CHOICE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                EVALUATE WS-SKILL-CHOICE
                    WHEN "1"
                        MOVE WS-UNDER-SKILL TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-UNDER-SKILL
                    WHEN "2"
                        MOVE WS-UNDER-SKILL TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-UNDER-SKILL
                    WHEN "3"
                        MOVE WS-UNDER-SKILL TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-UNDER-SKILL
                    WHEN "4"
                        MOVE WS-UNDER-SKILL TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-UNDER-SKILL
                    WHEN "5"
                        MOVE WS-UNDER-SKILL TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-UNDER-SKILL
                    WHEN "6"
                        MOVE 'Y' TO WS-EXIT-FLAG
                    WHEN OTHER
                        MOVE WS-INVALID-CHOICE TO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY WS-INVALID-CHOICE
                END-EVALUATE
                MOVE 'N' TO WS-EOF-FLAG
        END-READ
    END-PERFORM.
DISPLAY-SKILLS.
    MOVE WS-SKILL-TITLE TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-SKILL-TITLE
    MOVE "1. " TO OUTPUT-RECORD(1:3)
    MOVE WS-SKILL1 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "1. " WS-SKILL1
    MOVE "2. " TO OUTPUT-RECORD(1:3)
    MOVE WS-SKILL2 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "2. " WS-SKILL2
    MOVE "3. " TO OUTPUT-RECORD(1:3)
    MOVE WS-SKILL3 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "3. " WS-SKILL3
    MOVE "4. " TO OUTPUT-RECORD(1:3)
    MOVE WS-SKILL4 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "4. " WS-SKILL4
    MOVE "5. " TO OUTPUT-RECORD(1:3)
    MOVE WS-SKILL5 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "5. " WS-SKILL5
    MOVE "6. " TO OUTPUT-RECORD(1:3)
    MOVE WS-SKILL6 TO OUTPUT-RECORD(4:)
    WRITE OUTPUT-RECORD
    DISPLAY "6. " WS-SKILL6
    MOVE WS-CHOICE-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-CHOICE-PROMPT.
READ-NEXT-INPUT.
    READ INPUT-FILE INTO INPUT-RECORD
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
        NOT AT END
            CONTINUE
    END-READ.

SEARCH-FOR-USER.
    MOVE "Enter the full name of the person you are searching for (e.g., John Doe): " TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "Enter the full name of the person you are searching for (e.g., John Doe): "

    READ INPUT-FILE INTO WS-TEMP-INPUT
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
        NOT AT END
            MOVE FUNCTION TRIM(WS-TEMP-INPUT) TO WS-SEARCH-NAME
    END-READ

    MOVE 'N' TO WS-PROFILE-FOUND
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        CLOSE PROFILES-FILE
        MOVE "No profiles available." TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "No profiles available."
        EXIT PARAGRAPH
    END-IF

    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-PROFILE-FOUND = 'Y'
        READ PROFILES-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                MOVE SPACES TO WS-FULL-NAME
                STRING FUNCTION TRIM(PROFILE-FIRST-NAME)
                       " "
                       FUNCTION TRIM(PROFILE-LAST-NAME)
                       DELIMITED BY SIZE INTO WS-FULL-NAME
                END-STRING

                IF FUNCTION TRIM(WS-FULL-NAME) = FUNCTION TRIM(WS-SEARCH-NAME)
                    MOVE 'Y' TO WS-PROFILE-FOUND
                    MOVE PROFILE-FIRST-NAME TO WS-FIRST-NAME
                    MOVE PROFILE-LAST-NAME TO WS-LAST-NAME
                    MOVE PROFILE-UNIVERSITY TO WS-UNIVERSITY
                    MOVE PROFILE-MAJOR TO WS-MAJOR
                    MOVE PROFILE-GRAD-YEAR TO WS-GRAD-YEAR
                    MOVE PROFILE-ABOUT TO WS-ABOUT
                    MOVE PROFILE-EXP-COUNT TO WS-EXP-COUNT
                    MOVE PROFILE-EDU-COUNT TO WS-EDU-COUNT
                    MOVE 'Y' TO WS-VIEW-SEARCH
                    PERFORM VIEW-PROFILE
                    MOVE 'N' TO WS-VIEW-SEARCH
                END-IF
        END-READ
    END-PERFORM
    CLOSE PROFILES-FILE

    IF WS-PROFILE-FOUND = 'N'
        MOVE "No one by that name could be found." TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "No one by that name could be found."
    END-IF.

LOOKUP-USER-NAME.
    *> Initialize variables
    MOVE 'N' TO WS-USER-FOUND
    MOVE SPACES TO WS-LOOKUP-FIRST-NAME
    MOVE SPACES TO WS-LOOKUP-LAST-NAME
    MOVE SPACES TO WS-LOOKUP-FULL-NAME

    *> Open profiles file
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        CLOSE PROFILES-FILE
        MOVE WS-LOOKUP-USERNAME TO WS-LOOKUP-FULL-NAME
        EXIT PARAGRAPH
    END-IF

    *> Search for user profile
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-USER-FOUND = 'Y'
        READ PROFILES-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(PROFILE-USERNAME) = FUNCTION TRIM(WS-LOOKUP-USERNAME)
                    MOVE 'Y' TO WS-USER-FOUND
                    MOVE PROFILE-FIRST-NAME TO WS-LOOKUP-FIRST-NAME
                    MOVE PROFILE-LAST-NAME TO WS-LOOKUP-LAST-NAME
                END-IF
        END-READ
    END-PERFORM

    CLOSE PROFILES-FILE

    *> Build full name or use username if not found
    IF WS-USER-FOUND = 'Y'
        STRING FUNCTION TRIM(WS-LOOKUP-FIRST-NAME) " "
               FUNCTION TRIM(WS-LOOKUP-LAST-NAME)
               DELIMITED BY SIZE INTO WS-LOOKUP-FULL-NAME
    ELSE
        MOVE WS-LOOKUP-USERNAME TO WS-LOOKUP-FULL-NAME
    END-IF.

LOOKUP-USER-PROFILE-FOR-NETWORK.
    *> Initialize variables
    MOVE 'N' TO WS-USER-FOUND
    MOVE SPACES TO WS-LOOKUP-FIRST-NAME
    MOVE SPACES TO WS-LOOKUP-LAST-NAME
    MOVE SPACES TO WS-LOOKUP-FULL-NAME
    MOVE SPACES TO WS-LOOKUP-UNIVERSITY
    MOVE SPACES TO WS-LOOKUP-MAJOR

    *> Open profiles file
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        CLOSE PROFILES-FILE
        MOVE WS-LOOKUP-USERNAME TO WS-LOOKUP-FULL-NAME
        MOVE "Unknown" TO WS-LOOKUP-UNIVERSITY
        MOVE "Unknown" TO WS-LOOKUP-MAJOR
        EXIT PARAGRAPH
    END-IF

    *> Search for user profile
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-USER-FOUND = 'Y'
        READ PROFILES-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(PROFILE-USERNAME) = FUNCTION TRIM(WS-LOOKUP-USERNAME)
                    MOVE 'Y' TO WS-USER-FOUND
                    MOVE PROFILE-FIRST-NAME TO WS-LOOKUP-FIRST-NAME
                    MOVE PROFILE-LAST-NAME TO WS-LOOKUP-LAST-NAME
                    MOVE PROFILE-UNIVERSITY TO WS-LOOKUP-UNIVERSITY
                    MOVE PROFILE-MAJOR TO WS-LOOKUP-MAJOR
                END-IF
        END-READ
    END-PERFORM

    CLOSE PROFILES-FILE

    *> Build full name or use username if not found
    IF WS-USER-FOUND = 'Y'
        STRING FUNCTION TRIM(WS-LOOKUP-FIRST-NAME) " "
               FUNCTION TRIM(WS-LOOKUP-LAST-NAME)
               DELIMITED BY SIZE INTO WS-LOOKUP-FULL-NAME
    ELSE
        MOVE WS-LOOKUP-USERNAME TO WS-LOOKUP-FULL-NAME
        MOVE "Unknown" TO WS-LOOKUP-UNIVERSITY
        MOVE "Unknown" TO WS-LOOKUP-MAJOR
    END-IF.

VIEW-MY-PENDING-REQUESTS.
    MOVE "--- Pending Connection Requests ---" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "--- Pending Connection Requests ---"

    MOVE 'N' TO WS-CONNECTION-FOUND
    OPEN INPUT CONNECTIONS-FILE
    IF CONNECTIONS-STATUS = "35"
        CLOSE CONNECTIONS-FILE
        MOVE WS-NO-PENDING-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-NO-PENDING-MSG
        MOVE "-----------------------------------" TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "-----------------------------------"
        EXIT PARAGRAPH
    END-IF

    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y'
        READ CONNECTIONS-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(CONN-RECIPIENT-USERNAME) = FUNCTION TRIM(WS-USERNAME) AND
                   FUNCTION TRIM(CONN-STATUS) = "PENDING"
                    MOVE 'Y' TO WS-CONNECTION-FOUND
                    *> Lookup sender's full name
                    MOVE CONN-SENDER-USERNAME TO WS-LOOKUP-USERNAME
                    PERFORM LOOKUP-USER-NAME
                    MOVE SPACES TO OUTPUT-RECORD
                    STRING "Request from: " FUNCTION TRIM(WS-LOOKUP-FULL-NAME)
                        DELIMITED BY SIZE INTO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY "Request from: " FUNCTION TRIM(WS-LOOKUP-FULL-NAME)
                    IF FUNCTION TRIM(CONN-MESSAGE) NOT = SPACES
                        MOVE SPACES TO OUTPUT-RECORD
                        STRING "Message: " FUNCTION TRIM(CONN-MESSAGE)
                            DELIMITED BY SIZE INTO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY "Message: " FUNCTION TRIM(CONN-MESSAGE)
                    END-IF
                    
                    *> Close the file before updating
                    CLOSE CONNECTIONS-FILE
                    
                    *> Display accept/reject options
                    MOVE "1. Accept" TO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY "1. Accept"
                    MOVE "2. Reject" TO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY "2. Reject"
                    MOVE SPACES TO OUTPUT-RECORD
                    STRING "Enter your choice for " FUNCTION TRIM(WS-LOOKUP-FULL-NAME) ":"
                        DELIMITED BY SIZE INTO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY "Enter your choice for " FUNCTION TRIM(WS-LOOKUP-FULL-NAME) ":"
                    
                    *> Get user's choice
                    READ INPUT-FILE INTO WS-TEMP-INPUT
                        AT END
                            MOVE 'Y' TO WS-EOF-FLAG
                            EXIT PERFORM
                    END-READ
                    
                    MOVE FUNCTION UPPER-CASE(FUNCTION TRIM(WS-TEMP-INPUT)) TO WS-TEMP-INPUT
                    
                    *> Process the choice
                    IF WS-TEMP-INPUT = "1"
                        MOVE "ACCEPTED" TO WS-TEMP-INPUT
                        PERFORM UPDATE-CONNECTION-STATUS-FOR-PENDING
                        MOVE SPACES TO OUTPUT-RECORD
                        STRING "Connection request from " FUNCTION TRIM(WS-LOOKUP-FULL-NAME) " accepted!"
                            DELIMITED BY SIZE INTO OUTPUT-RECORD
                        WRITE OUTPUT-RECORD
                        DISPLAY "Connection request from " FUNCTION TRIM(WS-LOOKUP-FULL-NAME) " accepted!"
                    ELSE
                        IF WS-TEMP-INPUT = "2"
                            MOVE "REJECTED" TO WS-TEMP-INPUT
                            PERFORM UPDATE-CONNECTION-STATUS-FOR-PENDING
                            MOVE SPACES TO OUTPUT-RECORD
                            STRING "Connection request from " FUNCTION TRIM(WS-LOOKUP-FULL-NAME) " rejected!"
                                DELIMITED BY SIZE INTO OUTPUT-RECORD
                            WRITE OUTPUT-RECORD
                            DISPLAY "Connection request from " FUNCTION TRIM(WS-LOOKUP-FULL-NAME) " rejected!"
                        ELSE
                            MOVE "Invalid choice, request skipped." TO OUTPUT-RECORD
                            WRITE OUTPUT-RECORD
                            DISPLAY "Invalid choice, request skipped."
                        END-IF
                    END-IF
                    
                    MOVE "---" TO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY "---"
                    
                    *> Re-open the file to continue processing
                    OPEN INPUT CONNECTIONS-FILE
                END-IF
        END-READ
    END-PERFORM
    CLOSE CONNECTIONS-FILE

    IF WS-CONNECTION-FOUND = 'N'
        MOVE WS-NO-PENDING-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-NO-PENDING-MSG
    END-IF

    MOVE "-----------------------------------" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "-----------------------------------".
UPDATE-CONNECTION-STATUS-FOR-PENDING.
    OPEN INPUT CONNECTIONS-FILE
    OPEN OUTPUT TEMP-CONNECTIONS-FILE
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y'
        READ CONNECTIONS-FILE INTO TEMP-CONNECTION-RECORD
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(TEMP-CONN-SENDER-USERNAME) = FUNCTION TRIM(WS-LOOKUP-USERNAME) AND
                   FUNCTION TRIM(TEMP-CONN-RECIPIENT-USERNAME) = FUNCTION TRIM(WS-USERNAME) AND
                   FUNCTION TRIM(TEMP-CONN-STATUS) = "PENDING"
                    MOVE WS-TEMP-INPUT TO TEMP-CONN-STATUS
                END-IF
                WRITE TEMP-CONNECTION-RECORD
        END-READ
    END-PERFORM
    CLOSE CONNECTIONS-FILE
    CLOSE TEMP-CONNECTIONS-FILE

    OPEN OUTPUT CONNECTIONS-FILE
    OPEN INPUT TEMP-CONNECTIONS-FILE
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y'
        READ TEMP-CONNECTIONS-FILE INTO CONNECTION-RECORD
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                WRITE CONNECTION-RECORD
        END-READ
    END-PERFORM
    CLOSE CONNECTIONS-FILE
    CLOSE TEMP-CONNECTIONS-FILE.
   
SEARCH-FOR-USER-WITH-CONNECT.
    MOVE "Enter the full name of the person you are looking for:" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "Enter the full name of the person you are looking for:"

    READ INPUT-FILE INTO WS-TEMP-INPUT
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
        NOT AT END
            MOVE FUNCTION TRIM(WS-TEMP-INPUT) TO WS-SEARCH-NAME
    END-READ

    MOVE 'N' TO WS-PROFILE-FOUND
    OPEN INPUT PROFILES-FILE
    IF PROFILES-STATUS = "35"
        CLOSE PROFILES-FILE
        MOVE "No profiles available." TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "No profiles available."
        EXIT PARAGRAPH
    END-IF

    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR WS-PROFILE-FOUND = 'Y'
        READ PROFILES-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                MOVE SPACES TO WS-FULL-NAME
                STRING FUNCTION TRIM(PROFILE-FIRST-NAME)
                       " "
                       FUNCTION TRIM(PROFILE-LAST-NAME)
                       DELIMITED BY SIZE INTO WS-FULL-NAME
                END-STRING

                IF FUNCTION TRIM(WS-FULL-NAME) = FUNCTION TRIM(WS-SEARCH-NAME)
                    MOVE 'Y' TO WS-PROFILE-FOUND
                    MOVE PROFILE-FIRST-NAME TO WS-FIRST-NAME
                    MOVE PROFILE-LAST-NAME TO WS-LAST-NAME
                    MOVE PROFILE-UNIVERSITY TO WS-UNIVERSITY
                    MOVE PROFILE-MAJOR TO WS-MAJOR
                    MOVE PROFILE-GRAD-YEAR TO WS-GRAD-YEAR
                    MOVE PROFILE-ABOUT TO WS-ABOUT
                    MOVE PROFILE-EXP-COUNT TO WS-EXP-COUNT
                    MOVE PROFILE-EDU-COUNT TO WS-EDU-COUNT
                    PERFORM VARYING WS-EXP-IDX FROM 1 BY 1 UNTIL WS-EXP-IDX > 3
                        MOVE EXP-TITLE(WS-EXP-IDX) TO WS-EXP-TITLE(WS-EXP-IDX)
                        MOVE EXP-COMPANY(WS-EXP-IDX) TO WS-EXP-COMPANY(WS-EXP-IDX)
                        MOVE EXP-DATES(WS-EXP-IDX) TO WS-EXP-DATES(WS-EXP-IDX)
                        MOVE EXP-DESC(WS-EXP-IDX) TO WS-EXP-DESC(WS-EXP-IDX)
                    END-PERFORM
                    PERFORM VARYING WS-EDU-IDX FROM 1 BY 1 UNTIL WS-EDU-IDX > 3
                        MOVE EDU-DEGREE(WS-EDU-IDX) TO WS-EDU-DEGREE(WS-EDU-IDX)
                        MOVE EDU-UNIVERSITY(WS-EDU-IDX) TO WS-EDU-UNIVERSITY(WS-EDU-IDX)
                        MOVE EDU-YEARS(WS-EDU-IDX) TO WS-EDU-YEARS(WS-EDU-IDX)
                    END-PERFORM
                    MOVE PROFILE-USERNAME TO WS-TARGET-USERNAME
                    MOVE 'Y' TO WS-VIEW-SEARCH
                    PERFORM VIEW-PROFILE
                    MOVE 'N' TO WS-VIEW-SEARCH
                    PERFORM DISPLAY-CONNECTION-OPTIONS-FOR-USER
                END-IF
        END-READ
    END-PERFORM
    CLOSE PROFILES-FILE

    IF WS-PROFILE-FOUND = 'N'
        MOVE "No one by that name could be found." TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "No one by that name could be found."
    END-IF.

DISPLAY-CONNECTION-OPTIONS-FOR-USER.
    MOVE "1. Send Connection Request" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "1. Send Connection Request"
    MOVE "2. Back to Main Menu" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "2. Back to Main Menu"
    MOVE WS-CHOICE-PROMPT TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY WS-CHOICE-PROMPT

    READ INPUT-FILE INTO WS-CONN-CHOICE
        AT END
            MOVE 'Y' TO WS-EOF-FLAG
            EXIT PARAGRAPH
    END-READ

    EVALUATE WS-CONN-CHOICE
        WHEN "1"
            PERFORM SEND-CONNECTION-TO-FOUND-USER
        WHEN "2"
            CONTINUE
        WHEN OTHER
            MOVE WS-INVALID-CHOICE TO OUTPUT-RECORD
            WRITE OUTPUT-RECORD
            DISPLAY WS-INVALID-CHOICE
    END-EVALUATE.

SEND-CONNECTION-TO-FOUND-USER.
    IF WS-TARGET-USERNAME = WS-USERNAME
        MOVE WS-CONN-SELF-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-CONN-SELF-MSG
        EXIT PARAGRAPH
    END-IF

    PERFORM CHECK-CONNECTION-EXISTS-FOR-TARGET
    IF WS-CONN-ALREADY-CONNECTED = 'Y'
        MOVE WS-CONN-EXISTS-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-CONN-EXISTS-MSG
        EXIT PARAGRAPH
    END-IF
    IF WS-CONN-REQUEST-EXISTS = 'Y'
        MOVE WS-CONN-PENDING-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-CONN-PENDING-MSG
        EXIT PARAGRAPH
    END-IF

    PERFORM SAVE-CONNECTION-TO-TARGET
    MOVE SPACES TO OUTPUT-RECORD
    STRING "Connection request sent to " FUNCTION TRIM(WS-FULL-NAME) "."
        DELIMITED BY SIZE INTO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "Connection request sent to " FUNCTION TRIM(WS-FULL-NAME) ".".

CHECK-CONNECTION-EXISTS-FOR-TARGET.
    MOVE 'N' TO WS-CONN-REQUEST-EXISTS
    MOVE 'N' TO WS-CONN-ALREADY-CONNECTED
    OPEN INPUT CONNECTIONS-FILE
    IF CONNECTIONS-STATUS = "35"
        CLOSE CONNECTIONS-FILE
        EXIT PARAGRAPH
    END-IF
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y' OR (WS-CONN-REQUEST-EXISTS = 'Y' AND WS-CONN-ALREADY-CONNECTED = 'Y')
        READ CONNECTIONS-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF (FUNCTION TRIM(CONN-SENDER-USERNAME) = FUNCTION TRIM(WS-USERNAME) AND
                    FUNCTION TRIM(CONN-RECIPIENT-USERNAME) = FUNCTION TRIM(WS-TARGET-USERNAME)) OR
                   (FUNCTION TRIM(CONN-SENDER-USERNAME) = FUNCTION TRIM(WS-TARGET-USERNAME) AND
                    FUNCTION TRIM(CONN-RECIPIENT-USERNAME) = FUNCTION TRIM(WS-USERNAME))
                    IF FUNCTION TRIM(CONN-STATUS) = "PENDING"
                        MOVE 'Y' TO WS-CONN-REQUEST-EXISTS
                    END-IF
                    IF FUNCTION TRIM(CONN-STATUS) = "ACCEPTED"
                        MOVE 'Y' TO WS-CONN-ALREADY-CONNECTED
                    END-IF
                END-IF
        END-READ
    END-PERFORM
    CLOSE CONNECTIONS-FILE.

SAVE-CONNECTION-TO-TARGET.
    OPEN EXTEND CONNECTIONS-FILE
    MOVE SPACES TO CONNECTION-RECORD
    MOVE WS-USERNAME TO CONN-SENDER-USERNAME
    MOVE WS-TARGET-USERNAME TO CONN-RECIPIENT-USERNAME
    MOVE "PENDING" TO CONN-STATUS
    MOVE SPACES TO CONN-MESSAGE
    MOVE FUNCTION CURRENT-DATE(1:8) TO CONN-TIMESTAMP(1:8)
    MOVE FUNCTION CURRENT-DATE(9:6) TO CONN-TIMESTAMP(9:6)
    MOVE ":" TO CONN-TIMESTAMP(11:1)
    MOVE ":" TO CONN-TIMESTAMP(14:1)
    WRITE CONNECTION-RECORD
    CLOSE CONNECTIONS-FILE.

VIEW-MY-NETWORK.
    MOVE "--- Your Network ---" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "--- Your Network ---"
    
    MOVE 'N' TO WS-CONNECTION-FOUND
    OPEN INPUT CONNECTIONS-FILE
    IF CONNECTIONS-STATUS = "35"
        CLOSE CONNECTIONS-FILE
        MOVE WS-NO-CONNECTIONS-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-NO-CONNECTIONS-MSG
        MOVE "--------------------" TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY "--------------------"
        EXIT PARAGRAPH
    END-IF
    
    MOVE 'N' TO WS-EOF-FLAG
    PERFORM UNTIL WS-EOF-FLAG = 'Y'
        READ CONNECTIONS-FILE
            AT END
                MOVE 'Y' TO WS-EOF-FLAG
            NOT AT END
                IF FUNCTION TRIM(CONN-STATUS) = "ACCEPTED" AND
                   (FUNCTION TRIM(CONN-SENDER-USERNAME) = FUNCTION TRIM(WS-USERNAME) OR
                    FUNCTION TRIM(CONN-RECIPIENT-USERNAME) = FUNCTION TRIM(WS-USERNAME))
                    MOVE 'Y' TO WS-CONNECTION-FOUND
                    
                    *> Determine which user is the connection
                    IF FUNCTION TRIM(CONN-SENDER-USERNAME) = FUNCTION TRIM(WS-USERNAME)
                        MOVE CONN-RECIPIENT-USERNAME TO WS-LOOKUP-USERNAME
                    ELSE
                        MOVE CONN-SENDER-USERNAME TO WS-LOOKUP-USERNAME
                    END-IF
                    
                    *> Get connection's profile details
                    PERFORM LOOKUP-USER-PROFILE-FOR-NETWORK
                    
                    *> Display connection info
                    MOVE SPACES TO OUTPUT-RECORD
                    STRING "Connected with: " FUNCTION TRIM(WS-LOOKUP-FULL-NAME)
                           " (University: " FUNCTION TRIM(WS-LOOKUP-UNIVERSITY)
                           ", Major: " FUNCTION TRIM(WS-LOOKUP-MAJOR) ")"
                           DELIMITED BY SIZE INTO OUTPUT-RECORD
                    WRITE OUTPUT-RECORD
                    DISPLAY "Connected with: " FUNCTION TRIM(WS-LOOKUP-FULL-NAME)
                            " (University: " FUNCTION TRIM(WS-LOOKUP-UNIVERSITY)
                            ", Major: " FUNCTION TRIM(WS-LOOKUP-MAJOR) ")"
                END-IF
        END-READ
    END-PERFORM
    CLOSE CONNECTIONS-FILE
    
    IF WS-CONNECTION-FOUND = 'N'
        MOVE WS-NO-CONNECTIONS-MSG TO OUTPUT-RECORD
        WRITE OUTPUT-RECORD
        DISPLAY WS-NO-CONNECTIONS-MSG
    END-IF
    
    MOVE "--------------------" TO OUTPUT-RECORD
    WRITE OUTPUT-RECORD
    DISPLAY "--------------------".

CLEANUP.
    CLOSE INPUT-FILE
    CLOSE OUTPUT-FILE
    CLOSE ACCOUNTS-FILE
    CLOSE PROFILES-FILE
    CLOSE TEMP-PROFILES-FILE
    CLOSE CONNECTIONS-FILE
    CLOSE TEMP-CONNECTIONS-FILE.
END PROGRAM INCOLLEGE.